package nu.studer.gradle.rocker

import org.gradle.testkit.runner.TaskOutcome
import spock.lang.Unroll

@Unroll
class RockerFuncTest extends BaseFuncTest {

    void "can invoke rocker task derived from minimum configuration DSL"() {
        given:
        exampleRockerTemplate()

        and:
        buildFile << """
plugins {
    id 'nu.studer.rocker'
}

repositories {
    jcenter()
}

rocker {
  main {
    optimize = true
    templateDir = file('src/rocker')
    outputDir = file('src/generated/rocker')
  }
}
"""

        when:
        def result = runWithArguments('rockerMain')

        then:
        file('src/generated/rocker/Example.java').exists()
        result.output.contains("Parsing 1 rocker template files")
        result.output.contains("Generated 1 rocker java source files")
        result.output.contains("Generated rocker configuration")
        result.task(':rockerMain').outcome == TaskOutcome.SUCCESS
    }

    void "can compile Java source files generated by rocker as part of invoking Java compile task with the matching source set"() {
        given:
        exampleRockerTemplate()

        and:
        buildFile << """
plugins {
    id 'nu.studer.rocker'
    id 'java'
}

repositories {
    jcenter()
}

dependencies {
    compile 'com.fizzed:rocker-runtime:0.16.0'
}

rocker {
  main {
    optimize = true
    templateDir = file('src/rocker')
    outputDir = file('src/generated/rocker')
  }
}
"""

        when:
        def result = runWithArguments('classes')

        then:
        file('src/generated/rocker/Example.java').exists()
        file('build/classes/main/Example.class').exists()
        result.task(':rockerMain').outcome == TaskOutcome.SUCCESS
        result.task(':classes').outcome == TaskOutcome.SUCCESS
    }

    private File exampleRockerTemplate() {
        file('src/rocker/Example.rocker.html') << """
@args (String message)
Hello @message!
"""
    }

}
