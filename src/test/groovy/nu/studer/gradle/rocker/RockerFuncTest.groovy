package nu.studer.gradle.rocker

import org.gradle.testkit.runner.TaskOutcome
import spock.lang.Unroll

@Unroll
class RockerFuncTest extends BaseFuncTest {

    void "can invoke rocker task from minimum configuration DSL"() {
        given:
        file('src/rocker/Example.rocker.html') << """
@args (String message)
Hello @message!
"""

        buildFile << """
plugins {
    id 'nu.studer.rocker'
}

repositories {
    jcenter()
}

rocker {
  main {
    optimize = true
    templateDir = file('src/rocker')
    outputDir = file('src/generated/rocker')
  }
}
"""

        when:
        def result = runWithArguments('rockerMain')

        then:
        file('src/generated/rocker/Example.java').exists()
        result.output.contains("Parsing 1 rocker template files")
        result.output.contains("Generated 1 rocker java source files")
        result.output.contains("Generated rocker configuration")
        result.task(':rockerMain').outcome == TaskOutcome.SUCCESS
    }

    void "can compile Java source files generated by rocker"() {
        given:
        file('src/rocker/Example.rocker.html') << """
@args (String message)
Hello @message!
"""

        buildFile << """
plugins {
    id 'nu.studer.rocker'
}

apply plugin: 'java'

repositories {
    jcenter()
}

dependencies {
    compile 'com.fizzed:rocker-runtime:0.16.0'
}

rocker {
  main {
    optimize = true
    templateDir = file('src/rocker')
    outputDir = file('src/generated/rocker')
  }
}
"""

        when:
        def result = runWithArguments('classes')

        then:
        file('src/generated/rocker/Example.java').exists()
        result.output.contains("Generated 1 rocker java source files")
        result.task(':rockerMain').outcome == TaskOutcome.SUCCESS
        result.task(':classes').outcome == TaskOutcome.SUCCESS
    }

}
