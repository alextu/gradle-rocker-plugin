plugins {
    id 'groovy'
    id 'java-gradle-plugin'
    id 'nu.studer.plugindev' version '1.0.6'
    id 'nu.studer.credentials' version '1.0.3'
}

group = 'nu.studer'
version = '0.1-DEV'

sourceCompatibility = '1.6'
targetCompatibility = '1.6'

dependencies {
    testCompile('org.spockframework:spock-core:1.0-groovy-2.4') {
        exclude module: 'groovy-all'
    }
}

plugindev {
    pluginDescription 'Gradle plugin to invoke the Rocker template engine.'
    pluginImplementationClass 'nu.studer.gradle.rocker.RockerPlugin'
    pluginLicenses 'Apache-2.0'
    pluginTags 'gradle', 'plugin', 'rocker'
    authorId 'etiennestuder'
    authorName 'Etienne Studer'
    authorEmail 'etienne@studer.nu'
    projectUrl 'https://github.com/etiennestuder/gradle-rocker-plugin'
    projectInceptionYear '2017'
    done()
}

bintray {
    user = credentials.BINTRAY_USER
    key = credentials.BINTRAY_API_KEY
    pkg.repo = 'gradle-plugins'
    dryRun = true
}

def testAll = tasks.create('testAll') {
    group = 'Gradle cross-version test'
}

List<String> testedGradleVersions = []
(8..14).each { testedGradleVersions << "2.$it" }
(0..03).each { testedGradleVersions << "3.$it" }
testedGradleVersions << "2.14.1"
testedGradleVersions << "3.2.1"

testedGradleVersions.each { version ->
    project.tasks.create("test_" + version.replaceAll("[^a-zA-Z0-9]", "_"), Test).with {
        systemProperty 'testContext.gradleVersion', version
        testAll.dependsOn(it)
    }
}
