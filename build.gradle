plugins {
    id 'nu.studer.plugindev' version '4.0'
    id 'com.gradle.plugin-publish' version '0.15.0'
    id 'org.nosphere.gradle.github.actions' version '1.2.0'
    id 'groovy'
}

group = 'nu.studer'
version = '3.0.3-DEV'

dependencies {
    testImplementation 'org.spockframework:spock-core:2.0-groovy-3.0'
}

tasks.withType(AbstractCompile).configureEach {
    options.compilerArgs <<
        "-Werror" <<
        "-Xlint:all"
}

tasks.withType(Test).configureEach {
    maxParallelForks = 2
    useJUnitPlatform()
}

def testAll = tasks.create('testAll') {
    description = 'Runs the Gradle cross-version tests.'
    group = 'Verification'
}

List<String> testedGradleVersions = []
testedGradleVersions << '7.2'
testedGradleVersions << '7.0.2'
testedGradleVersions << '6.9'
testedGradleVersions << '6.1.1'

testedGradleVersions.each { version ->
    project.tasks.create("test_" + version.replaceAll("[^a-zA-Z0-9]", "_"), Test).with {
        systemProperty 'testContext.gradleVersion', version
        testAll.dependsOn(it)
    }
}

gradlePlugin {
    plugins {
        pluginDevPlugin {
            id = 'nu.studer.rocker'
            implementationClass = 'nu.studer.gradle.rocker.RockerPlugin'
        }
    }
}

pluginBundle {
    website = 'https://github.com/etiennestuder/gradle-rocker-plugin'
    vcsUrl = 'https://github.com/etiennestuder/gradle-rocker-plugin'
    description = 'Gradle plugin that integrates the Rocker template engine.'
    tags = ['rocker']

    plugins {
        pluginDevPlugin {
            displayName = 'gradle-rocker-plugin'
        }
    }

    mavenCoordinates {
        groupId = 'nu.studer'
        artifactId = 'gradle-rocker-plugin'
    }
}
